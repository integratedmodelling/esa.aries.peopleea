project private namespace peopleea.forest.biomass
	using (BIO_REGION_FOREST_REFERENCES_ABOVE_GROUND_BIOMASS) from peopleea.tables;

@intensive(space)   
@extensive(time)
model 'local:stefano.balbi:im.openeo.sandbox:vito.biomas.cci#output_warp=true&synchronous=true'
  	as ecology:AboveGroundBiomass in t/ha;

@intensive(space)   
@extensive(time)
model 'local:alessio.bulckaen:im.openeo.sandbox:vito.esa.biomass_100m#output_warp=true&synchronous=true' 
	as ecology:Vegetation ecology:Biomass in t/ha; 

number max_above_ground_biomass 
	observing
		es.nca:BiogeographicRegionType named bioregion,
		type of ecology:Forest named forest_type
	lookup (bioregion, forest_type, ?, *) 
		into BIO_REGION_FOREST_REFERENCES_ABOVE_GROUND_BIOMASS; 

number min_above_ground_biomass
	observing
		es.nca:BiogeographicRegionType named bioregion,
		type of ecology:Forest named forest_type
	lookup (bioregion, forest_type, *, ?) 
		into BIO_REGION_FOREST_REFERENCES_ABOVE_GROUND_BIOMASS; 

@colormap(colors = ((211, 118, 118) (241, 239, 153) (176, 197, 164)))
model im:Indicator value of ecology:AboveGroundBiomass 
	observing 
		max_above_ground_biomass,
		min_above_ground_biomass,
		ecology:AboveGroundBiomass named above_ground_biomass
	set to [above_ground_biomass < min_above_ground_biomass ? 0 : ( above_ground_biomass > max_above_ground_biomass ? 1 : ((above_ground_biomass - min_above_ground_biomass)/(max_above_ground_biomass - min_above_ground_biomass)) )];

@documented(peopleea.forest_condition_accounts.variable)
@var(timespan=(start end))	
define table agb_raw_variable as {
	name: "above_ground_biomass_variable"
	title: "Above Ground Biomass Variable Account (t/ha)",
	label: "Above Ground Biomass Variable",
	target: ecology:AboveGroundBiomass where (any type of ecology:Forest)
	observables: (type of ecology:Forest, es.nca:BiogeographicRegionType)
	when: end
	use: tables.compiler.summarize(contabilize-nulls = true, 
		rowsummary = { filter: "change" title: "Net change"}, 
//		colsummary=  { filter: "mean", title: "Mean" },
		rows =       { filter: %timespan% }, 
		columns =    { filter: (type of ecology:Forest, es.nca:BiogeographicRegionType)}, 
		statistic =  "mean",
		empty='', 
		no-data = ''
	)
};

@documented(peopleea.forest_condition_accounts.indicators)
@var(timespan=(start end))	
define table agb_indicator as {
	name: "above_ground_biomass_indicator"
	title: "Above Ground Biomass Indicator Account",
	label: "Above Ground Biomass Indicator",
	target: im:Indicator value of ecology:AboveGroundBiomass
	observables: (type of ecology:Forest, es.nca:BiogeographicRegionType)
	when: end
	use: tables.compiler.summarize(contabilize-nulls = true, 
		rowsummary = { filter: "change" title: "Net change"}, 
//		colsummary=  { filter: "mean", title: "Mean" },
		rows =       { filter: %timespan% }, 
		columns =    { filter: (type of ecology:Forest, es.nca:BiogeographicRegionType)}, 
		statistic =  "mean",
		empty='', 
		no-data = ''
	)
};	