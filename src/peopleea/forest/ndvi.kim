project private namespace peopleea.forest.ndvi
	using (BIO_REGION_MAES_FOREST_PRODUCTIVITY_NDVI) from peopleea.tables;

model 'local:ferdinando.villa:im.openeo.sandbox:vito.ndvi#output_warp=true&synchronous=true'
	as ecology:NormalizedDifferenceVegetationIndex;

number max_forest_productivity_ndvi  
	observing
		es.nca:BiogeographicRegionType named bioregion,
		type of ecology:Forest named forest_type
	lookup (bioregion, forest_type, ?, *, *, *) 
		into BIO_REGION_MAES_FOREST_PRODUCTIVITY_NDVI;   

number min_forest_productivity_ndvi  
	observing
		es.nca:BiogeographicRegionType named bioregion,
		type of ecology:Forest named forest_type
	lookup (bioregion, forest_type, *, ?, *, *) 
		into BIO_REGION_MAES_FOREST_PRODUCTIVITY_NDVI; 

//@key(en="NDVI condition indicator", es="Índice NDVI de vegetación")
model im:Indicator value of ecology:NormalizedDifferenceVegetationIndex 	
	observing 
		max_forest_productivity_ndvi,
		min_forest_productivity_ndvi,
        ecology:NormalizedDifferenceVegetationIndex named forest_productivity_ndvi
	set to [forest_productivity_ndvi < min_forest_productivity_ndvi ? 0 : ( forest_productivity_ndvi > max_forest_productivity_ndvi ? 1 : ((forest_productivity_ndvi - min_forest_productivity_ndvi)/(max_forest_productivity_ndvi - min_forest_productivity_ndvi)) )];

@documented(peopleea.forest_condition_accounts.variable)	
@var(timespan=(start end))	
define table ndvi_raw_variable as {
	name: "ndvi_variable"
	title: "NDVI Variable Account",
	label: "NDVI Variable",
	target: ecology:NormalizedDifferenceVegetationIndex where (any type of ecology:Forest)
	observables: (type of ecology:Forest, es.nca:BiogeographicRegionType)
	when: end
	use: tables.compiler.summarize(contabilize-nulls = true, 
		rowsummary = { filter: "change" title: "Net change"}, 
//		colsummary=  { filter: "mean", title: "Mean" },
		rows =       { filter: %timespan% }, 
		columns =    { filter: (type of ecology:Forest, es.nca:BiogeographicRegionType)}, 
		statistic =  "mean",
		empty='', 
		no-data = ''
	)
};

@documented(peopleea.forest_condition_accounts.indicators)
@var(timespan=(start end))	
define table ndvi_indicator as {
	name: "ndvi_indicator"
	title: "NDVI Indicator Account",
	label: "NDVI Indicator",
	target: im:Indicator value of ecology:NormalizedDifferenceVegetationIndex 
	observables: (type of ecology:Forest, es.nca:BiogeographicRegionType)
	when: end
	use: tables.compiler.summarize(contabilize-nulls = true, 
		rowsummary = { filter: "change" title: "Net change"}, 
//		colsummary=  { filter: "mean", title: "Mean" },
		rows =       { filter: %timespan% }, 
		columns =    { filter: (type of ecology:Forest, es.nca:BiogeographicRegionType)}, 
		statistic =  "mean",
		empty='', 
		no-data = ''
	)
};