project private namespace peopleea.forest.lai
	using (BIO_REGION_FOREST_REFERENCES_LEAF_AREA_INDEX) from peopleea.tables;

model 'local:ferdinando.villa:im.openeo.sandbox:vito.lai#output_warp=true&synchronous=true'
	 as ecology:LeafAreaIndex; 
	 
/*LAI Tier 2 */	
model 'local:stefano.balbi:esa.aries.peopleea:vito.lai.10m#output_warp=true&synchronous=true'
	as ecology:LeafAreaIndex named lai_10m;

number max_leaf_area_index  
	observing
		es.nca:BiogeographicRegionType named bioregion,
		type of ecology:Forest named forest_type
	lookup (bioregion, forest_type, ?, *) 
		into BIO_REGION_FOREST_REFERENCES_LEAF_AREA_INDEX; 
		
number min_leaf_area_index  
	observing
		es.nca:BiogeographicRegionType named bioregion,
		type of ecology:Forest named forest_type
	lookup (bioregion, forest_type, *, ?) 
		into BIO_REGION_FOREST_REFERENCES_LEAF_AREA_INDEX; 

 model im:Indicator value of ecology:LeafAreaIndex	
	observing 
		min_leaf_area_index,
		max_leaf_area_index,
        ecology:LeafAreaIndex named leaf_area_index
	set to [leaf_area_index < min_leaf_area_index ? 0 : ( leaf_area_index > max_leaf_area_index ? 1 : ((leaf_area_index - min_leaf_area_index)/(max_leaf_area_index - min_leaf_area_index)) )];

@documented(peopleea.forest_condition_accounts.variable)	
@var(timespan=(start end))	
define table lai_raw_variable as {
	name: "lai_variable"
	title: "Leaf Area Index Variable Account",
	label: "Leaf Area Index Variable",
	target: ecology:LeafAreaIndex where (type of ecology:Forest)
	observables: (type of ecology:Forest, es.nca:BiogeographicRegionType)
	when: end
	use: tables.compiler.summarize(contabilize-nulls = true, 
		rowsummary = { filter: "change" title: "Net change"}, 
//		colsummary=  { filter: "mean", title: "Mean" },
		rows =       { filter: %timespan% }, 
		columns =    { filter: (type of ecology:Forest, es.nca:BiogeographicRegionType)}, 
		statistic =  "mean",
		empty='', 
		no-data = ''
	)
};

@documented(peopleea.forest_condition_accounts.indicators)
@var(timespan=(start end))	
define table lai_indicator as {
	name: "lai_indicator"
	title: "Leaf Area Index Indicator Account",
	label: "Leaf Area Index Indicator",
	target: im:Indicator value of ecology:LeafAreaIndex
	observables: (type of ecology:Forest, es.nca:BiogeographicRegionType)
	when: end
	use: tables.compiler.summarize(contabilize-nulls = true, 
		rowsummary = { filter: "change" title: "Net change"}, 
//		colsummary=  { filter: "mean", title: "Mean" },
		rows =       { filter: %timespan% }, 
		columns =    { filter: (type of ecology:Forest, es.nca:BiogeographicRegionType)}, 
		statistic =  "mean",
		empty='', 
		no-data = ''
	)
};