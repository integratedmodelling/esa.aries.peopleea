project private namespace peopleea.soil.carbon
	using (BIO_REGION_FOREST_REFERENCES_SOIL_ORGANIC_CARBON_INSPIRE) from peopleea.tables;

model 'local:belen.inguanzo:esa.people.sandbox:im-data-global-soil.esdac_inspire_topsoil_organic_carbon_content_of_europe_2003', 
	  'local:rubencc:esa.people.sandbox:im-data-global-soil.esdac_lucas_topsoil_soil_organic_carbon_of_eu_2014'
	  as percentage of soil:TopSoil chemistry:Organic chemistry:Carbon im:Mass;  

number max_soil_organic_carbon_inspire  
	observing
		es.nca:BiogeographicRegionType named bioregion,
		type of ecology:Forest named forest_type
	lookup (bioregion, forest_type, ?, *, *, *) 
		into BIO_REGION_FOREST_REFERENCES_SOIL_ORGANIC_CARBON_INSPIRE; 

number min_soil_organic_carbon_inspire  
	observing
		es.nca:BiogeographicRegionType named bioregion,
		type of ecology:Forest named forest_type
	lookup (bioregion, forest_type, *, ?, *, *) 
		into BIO_REGION_FOREST_REFERENCES_SOIL_ORGANIC_CARBON_INSPIRE;  

@colormap(colors = ((211, 118, 118) (241, 239, 153) (176, 197, 164)))
model im:Indicator value of soil:TopSoil chemistry:Organic chemistry:Carbon im:Mass
	observing 
		max_soil_organic_carbon_inspire,
		min_soil_organic_carbon_inspire,
		percentage of soil:TopSoil chemistry:Organic chemistry:Carbon im:Mass named soil_organic_carbon 
	set to [soil_organic_carbon < min_soil_organic_carbon_inspire ? 0 : ( soil_organic_carbon > max_soil_organic_carbon_inspire ? 1 : ((soil_organic_carbon - min_soil_organic_carbon_inspire)/(max_soil_organic_carbon_inspire - min_soil_organic_carbon_inspire)) )];

@documented(peopleea.forest_condition_accounts.variable)
@var(timespan=(start end))	
define table soc_raw_variable as {
	name: "soil_organic_carbon_variable"
	title: "Soil Organic Carbon Variable Account (%)",
	label: "Soil Organic Carbon Variable",
	target: percentage of soil:TopSoil chemistry:Organic chemistry:Carbon im:Mass
	observables: (type of ecology:Forest, es.nca:BiogeographicRegionType)
	when: end
	use: tables.compiler.summarize(contabilize-nulls = true, 
		rowsummary = { filter: "change" title: "Net change"}, 
//		colsummary=  { filter: "mean", title: "Mean" },
		rows =       { filter: %timespan% }, 
		columns =    { filter: (type of ecology:Forest, es.nca:BiogeographicRegionType)}, 
		statistic =  "mean",
		empty='', 
		no-data = ''
	)
};

@documented(peopleea.forest_condition_accounts.indicators)
@var(timespan=(start end))	
define table soc_indicator as {
	name: "soil_organic_carbon_indicator"
	title: "Soil Organic Carbon Indicator Account",
	label: "Soil Organic Carbon Indicator",
	target: im:Indicator value of soil:TopSoil chemistry:Organic chemistry:Carbon im:Mass
	observables: (type of ecology:Forest, es.nca:BiogeographicRegionType)
	when: end
	use: tables.compiler.summarize(contabilize-nulls = true, 
		rowsummary = { filter: "change" title: "Net change"}, 
//		colsummary=  { filter: "mean", title: "Mean" },
		rows =       { filter: %timespan% }, 
		columns =    { filter: (type of ecology:Forest, es.nca:BiogeographicRegionType)}, 
		statistic =  "mean",
		empty='', 
		no-data = ''
	)
};